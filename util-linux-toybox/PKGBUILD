# $Id: PKGBUILD 221785 2014-09-15 20:51:32Z dreisner $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=util-linux-toybox
pkgname=(util-linux-toybox libutil-linux)
pkgver=2.25.2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('i686' 'x86_64')
makedepends=('systemd' 'python')
license=('GPL2')
options=('strip' 'debug')
source=("ftp://ftp.kernel.org/pub/linux/utils/util-linux/v2.25/util-linux-$pkgver.tar.xz"
        uuidd.tmpfiles
        pam-{login,common,su})
md5sums=('2ff36a8f8ede70f66c5ad0fb09e40e79'
         'a39554bfd65cccfd8254bb46922f4a67'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd "util-linux-$pkgver"

  ./configure --disable-all-programs \
              --prefix=/usr \
              --libdir=/usr/lib \
              --bindir=/usr/bin \
              --localstatedir=/run \
              --enable-fs-paths-extra=/usr/bin \
              --enable-libblkid \
              --enable-libmount \
              --enable-libsmartcols \
              --enable-libuuid \
              --enable-partx \
              --enable-uuidd \
              --enable-nsenter \
              --enable-agetty \
              --enable-cramfs \
              --enable-bfs \
              --enable-minix \
              --enable-fdformat \
              --enable-hwclock \
              --enable-wdctl \
              --enable-utmpdump \
              --enable-mesg \
              --enable-rename \
              --enable-vipw \
              --enable-newgrp \
              --enable-chfn-chsh \
              --enable-login \
              --enable-nologin \
              --enable-sulogin \
              --enable-su \
              --enable-runuser \
              --enable-ul \
              --enable-pg \
              --enable-setterm \
              --enable-schedutils \
              --enable-wall \
              --enable-write \
              --enable-socket-activation \
              --enable-getopt \
              --with-python=3
  make
}

package_util-linux-toybox() {
  conflicts=('util-linux-ng' 'eject')
  provides=("util-linux-ng=$pkgver" 'eject')
  depends=('pam' 'shadow' 'coreutils' 'libsystemd' 'libutil-linux')
  optdepends=('python: python bindings to libmount')
  install=util-linux.install
  groups=('base' 'base-devel')
  backup=(etc/pam.d/chfn
          etc/pam.d/chsh
          etc/pam.d/login
          etc/pam.d/su
          etc/pam.d/su-l)

  cd "util-linux-$pkgver"

  make DESTDIR="${pkgdir}" install

  # setuid chfn and chsh
  chmod 4755 "$pkgdir"/usr/bin/{newgrp,ch{sh,fn}}

  # install PAM files for login-utils
  install -Dm644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chfn"
  install -m644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chsh"
  install -m644 "$srcdir/pam-login" "$pkgdir/etc/pam.d/login"
  install -m644 "$srcdir/pam-su" "${pkgdir}/etc/pam.d/su"
  install -m644 "$srcdir/pam-su" "${pkgdir}/etc/pam.d/su-l"

  # include tmpfiles fragment for uuidd
  # TODO(dreisner): offer this upstream?
  install -Dm644 "$srcdir/uuidd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/uuidd.conf"

  # usrmove
  cd "$pkgdir"
  mv {,usr/}sbin/* usr/bin
  rmdir sbin usr/sbin

  ### create libutil-linux split
  rm -rf "$srcdir/_libutil-linux"
  install -dm755 "$srcdir"/_libutil-linux/usr/lib
  cd "$srcdir"/_libutil-linux
  mv "$pkgdir"/usr/lib/lib*.{a,so}* usr/lib
}

package_libutil-linux() {
  pkgdesc="util-linux runtime libraries"
  provides=('libblkid.so' 'libmount.so' 'libsmartcols.so' 'libuuid.so')

  mv "$srcdir/_libutil-linux"/* "$pkgdir"
}
md5sums=('cab3d7be354000f629bc601238b629b3'
         'a39554bfd65cccfd8254bb46922f4a67'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'fa85e5cce5d723275b14365ba71a8aad')
